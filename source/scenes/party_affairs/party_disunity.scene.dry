title: Party Disunity
new-page: true
is-card: true
view-if: party_disunity_timer = 0 and (dissent >= 0.3 or (reformist_dissent > 30 and not reformists_resign) or (center_dissent > 30 and not centrists_resign) or (left_dissent > 30 and not sapd_formed) or (labor_dissent > 30 and not unions_independent) or (neorevisionist_dissent > 30 and neorevisionism))
on-arrival: month_actions += 1; party_disunity_timer = 4
card-image: img/Reichstagsfraktion_der_SPD.jpg
frequency: 200
tags: party_affairs

# TODO: party dissent events for every faction, have a good image for this... 

= Party Disunity

The party is being torn apart by internal dissent. Upset at the leadership and general direction, factions have started sabotaging their rivals, hampering our efforts to win votes and carry out our policies.

[? if reformist_dissent > 30 : The reformists inform us that their capitalist masters are upset at our direction.  ?]
[? if center_dissent > 30 : The centrists are displeased with the continued existence of the rest of our party. ?]
[? if labor_dissent > 30 : The trade unions disagree with our present course of inaction. ?]
[? if left_dissent > 30: Liberals are preparing for another party split. ?]
[? if neorevisionist_dissent > 30 and neorevisionism = 1 : Neorevisionists are slowly realizing that social fascists are still fascists. ?]

Something must be done to reinforce party unity.

- @enforce_unity
- @concessions_reformists
- @concessions_center
- @concessions_left
- @concessions_labor
- @concessions_neorevisionists
- @do_nothing


@enforce_unity
title: Enforce party discipline.
on-arrival: reformist_dissent -= 5; center_dissent -= 5; labor_dissent -= 5; left_dissent -= 5; neorevisionist_dissent -= 5; workers_spd -= 4; new_middle_spd -= 2; unemployed_spd -= 2

Did our leadership make mistakes? No, it is the faction members who are wrong. We expel the critics and praise ourselves on solving the problem.

# enforcing party discipline applies to all parties.

# each party has a concessions action and an expulsions action.

@concessions_reformists
title: Give concessions to the bourgeois lackeys.
view-if: reformist_dissent > 30
on-arrival: reformist_dissent -= 7; left_dissent += 5

We have promised to adopt a more supine posture. This may alienate anyone who's not on the corporate payroll, however.

@concessions_center
title: Give concessions to the centrists.
view-if: center_dissent > 30
on-arrival: center_dissent -= 7; reformist_dissent += 2; left_dissent += 2; neorevisionist_dissent += 2

We have promised the centrists to take even less action. Both the liberal and capitalist wings of the party might be displeased, but what are they going to do about it?

@concessions_left
title: Give concessions to the liberals.
view-if: left_dissent > 30
on-arrival: left_dissent -= 7; reformist_dissent += 5

We have lied that we will listen to what passes for the left wing of the party. Of course, even this empty promise leads to increased dissent from the centrists.

@concessions_labor
title: Give concessions to labor.
view-if: labor_dissent > 30
on-arrival: labor_dissent -= 7; center_dissent += 5

The trade unions will be granted more influence in party decision making. This upsets the centrists, who would prefer to eliminate political decision making within the party.

@concessions_neorevisionists
title: Give concessions to the neorevisionists.
view-if: neorevisionist_dissent > 30 and neorevisionism = 1
on-arrival: neorevisionist_dissent -= 7; center_dissent += 5;

Giving the neorevisionists more influence will cause increased dissent from the centrists, who are understandably alarmed by the idea of fascists facing any consequences.


@do_nothing
title: Our response is, as always, to do nothing.
on-arrival: labor_dissent += 1; left_dissent += 1; center_dissent += 1; reformist_dissent += 1

Let the factions bicker; perhaps circular firing squads are healthy for the party.

# TODO: should we be allowed to preemptively expel factions?

@expel_reformists
title: Expel the dissident reformists.
view-if: reformist_dissent > 30

@expel_left
title: Expel the dissident leftists.
view-if: left_dissent > 30

@expel_center
title: Expel the centrists.
view-if: center_dissent > 30

@expel_labor
title: Expel the labor supporters.
view-if: labor_dissent > 30
